	include	'Header.a'

	; Delete the tempfile just in case a previous run was interrupted
	bsr		PushPB
	pea		TheFileName
	move.l	(sp)+,$12(a0) ; ioNamePtr
	dc.w	$a009
	bsr		PopPB

	StartOfTest 'CreateResFile TestToolbox.tmp; check ResError'
	bsr		PoisonResError
	pea		TheFileName
	dc.w	$a9b1 ; _CreateResFile
	tst.w	$a60
	z1
	EndOfTest

	StartOfTest 'CreateResFile TestToolbox.tmp; check ResError, expecting dupFNErr'
	bsr		PoisonResError
	pea		TheFileName
	dc.w	$a9b1 ; _CreateResFile
	cmp.w	#-48,$a60
	z1
	EndOfTest

	StartOfTest 'OpenResFile TestToolbox.tmp; check result matches CurMap; check ResError'
	bsr		PoisonResError
	move.w	#$b33f,-(sp)
	pea		TheFileName
	dc.w	$a997 ; _OpenResFile
	move.w	(sp)+,d7 ; keep the refnum in d7
	cmp.w	$a5a,d7
	z1
	tst.w	$a60
	z1
	EndOfTest

	StartOfTest 'Count1Types, expecting 0'
	move.w	#$b33f,-(sp)
	dc.w	$a81c ; _Count1Types
	tst.w	(sp)+
	z1
	EndOfTest

	StartOfTest 'NewHandle'
	moveq.l	#4,d0
	dc.w	$a122 ; _NewHandle
	z1
	move.l	a0,a4 ; keep the handle in a4
	EndOfTest

	move.l	(a4),a0 ; populate the handle
	move.l	#'4byt',(a0)

	StartOfTest 'AddResource; check ResError'
	bsr		PoisonResError
	move.l	a4,-(sp)
	move.l	#'Test',-(sp)
	move.w	#128,-(sp)
	pea		TheResourceName
	dc.w	$a9ab
	tst.w	$a60
	z1
	EndOfTest

	StartOfTest 'Count1Types, expecting 1'
	move.w	#$b33f,-(sp)
	dc.w	$a81c ; _Count1Types
	cmp.w	#1,(sp)+
	z1
	EndOfTest

	StartOfTest 'CloseResFile; check ResError'
	bsr		PoisonResError
	move.w	d7,-(sp)
	dc.w	$a99a
	tst.w	$a60
	z1
	EndOfTest

	StartOfTest 'Delete TestToolbox.tmp'
	bsr		PushPB
	pea		TheFileName
	move.l	(sp)+,$12(a0) ; ioNamePtr
	dc.w	$a009
	z1
	bsr		PopPB
	EndOfTest

	StartOfTest 'Delete TestToolbox.tmp again, expecting fnfErr'
	bsr		PushPB
	pea		TheFileName
	move.l	(sp)+,$12(a0) ; ioNamePtr
	dc.w	$a009
	cmp.w	#-43,d0
	z1
	bsr		PopPB
	EndOfTest

	PrintPlan
	rts

TheFileName
	dc.b	15, 'TestToolbox.tmp'

TheResourceName
	dc.b	17, 'The Resource Name'

PushPB
	move.l	(sp)+,a1
	moveq	#31,d0
.loop
	clr.l	-(sp)
	dbra	d0,.loop
	move.l	sp,a0
	jmp		(a1)

PopPB
	move.l	(sp)+,a0
	add.w	#128,sp
	jmp		(a0)

PoisonResError
	move.w	#$ffff,$a60
	rts
