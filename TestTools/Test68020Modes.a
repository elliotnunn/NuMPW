; Copyright (c) 2021 Elliot Nunn
; Licensed under the MIT license

			MAIN
			MACHINE	MC68020
			OPT		NONE
			IMPORT	TestOpen, TestClose, TestPlan, TestStrCmp
			IMPORT	TestX0, TestX1, TestN0, TestN1, TestZ0, TestZ1, TestV0, TestV1, TestC0, TestC1

			lea		bigtab,a0
			lea		bigtabptr,a1
			move.l	a0,(a1)

			pea		#'(d8,an,xn.w)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#1,(3,a4,d4.w)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.l)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#-2,d4
			adda.l	#$66660000,a4
			sub.l	#$66660000,d4
			cmpi.b	#1,(3,a4,d4.l)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.w*2)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#-1,(3,a4,d4.w*2)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.l*2)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#-2,d4
			adda.l	#$66660000,a4
			sub.l	#$66660000/2,d4
			cmpi.b	#-1,(3,a4,d4.l*2)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.w*4)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#-5,(3,a4,d4.w*4)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.l*4)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#-2,d4
			adda.l	#$66660000,a4
			sub.l	#$66660000/4,d4
			cmpi.b	#-5,(3,a4,d4.l*4)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.w*8)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#-13,(3,a4,d4.w*8)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,an,xn.l*8)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#-2,d4
			adda.l	#$66660000,a4
			sub.l	#$66660000/8,d4
			cmpi.b	#-13,(3,a4,d4.l*8)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(bd,an,xn.l*2)'
			jsr		TestOpen
			lea		bigtab,a4
			move.l	#250,d4
			cmpi.b	#5,(-500+5,a4,d4.l*2)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,pc,xn.w)'
			jsr		TestOpen
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#-2,(bigtab,pc,d4.w)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,pc,xn.l)'
			jsr		TestOpen
			move.l	#-2,d4
			cmpi.b	#-2,(bigtab,pc,d4.l)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,pc,xn.w*8)'
			jsr		TestOpen
			move.l	#$6666fffe,d4 ; -2
			cmpi.b	#-16,(bigtab,pc,d4.w*8)
			jsr		TestZ1
			jsr		TestClose

			pea		#'(d8,pc,xn.l*8)'
			jsr		TestOpen
			move.l	#-2,d4
			cmpi.b	#-16,(bigtab,pc,d4.l*8)
			jsr		TestZ1
			jsr		TestClose

			bra		skipBigtab

			dc.b	$80,$81,$82,$83,$84,$85,$86,$87,$88,$89,$8a,$8b,$8c,$8d,$8e,$8f
			dc.b	$90,$91,$92,$93,$94,$95,$96,$97,$98,$99,$9a,$9b,$9c,$9d,$9e,$9f
			dc.b	$a0,$a1,$a2,$a3,$a4,$a5,$a6,$a7,$a8,$a9,$aa,$ab,$ac,$ad,$ae,$af
			dc.b	$b0,$b1,$b2,$b3,$b4,$b5,$b6,$b7,$b8,$b9,$ba,$bb,$bc,$bd,$be,$bf
			dc.b	$c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7,$c8,$c9,$ca,$cb,$cc,$cd,$ce,$cf
			dc.b	$d0,$d1,$d2,$d3,$d4,$d5,$d6,$d7,$d8,$d9,$da,$db,$dc,$dd,$de,$df
			dc.b	$e0,$e1,$e2,$e3,$e4,$e5,$e6,$e7,$e8,$e9,$ea,$eb,$ec,$ed,$ee,$ef
			dc.b	$f0,$f1,$f2,$f3,$f4,$f5,$f6,$f7,$f8,$f9,$fa,$fb,$fc,$fd,$fe,$ff
bigtab
			dc.b	$00,$01,$02,$03,$04,$05,$06,$07,$08,$09,$0a,$0b,$0c,$0d,$0e,$0f
			dc.b	$10,$11,$12,$13,$14,$15,$16,$17,$18,$19,$1a,$1b,$1c,$1d,$1e,$1f
			dc.b	$20,$21,$22,$23,$24,$25,$26,$27,$28,$29,$2a,$2b,$2c,$2d,$2e,$2f
			dc.b	$30,$31,$32,$33,$34,$35,$36,$37,$38,$39,$3a,$3b,$3c,$3d,$3e,$3f
			dc.b	$40,$41,$42,$43,$44,$45,$46,$47,$48,$49,$4a,$4b,$4c,$4d,$4e,$4f
			dc.b	$50,$51,$52,$53,$54,$55,$56,$57,$58,$59,$5a,$5b,$5c,$5d,$5e,$5f
			dc.b	$60,$61,$62,$63,$64,$65,$66,$67,$68,$69,$6a,$6b,$6c,$6d,$6e,$6f
			dc.b	$70,$71,$72,$73,$74,$75,$76,$77,$78,$79,$7a,$7b,$7c,$7d,$7e,$7f
bigtabptr
			dc.l	0 ; fill this with address of bigtab

skipBigtab

			pea		#'([bd,an],xn.w*2,od)'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$66660000,a4
			move.l	#$66660002,d4
			cmpi.b	#7,([-$66660000,a4],d4.w*2,3)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([an],xn.w*2,od)'
			jsr		TestOpen
			lea		bigtabptr,a4
			move.l	#$66660002,d4
			cmpi.b	#7,([a4],d4.w*2,3)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([bd],xn.w*2,od)'
			jsr		TestOpen
			lea		bigtab,a0
			move.l	a0,$a78 ; ApplScratch
			move.l	#$66660002,d4
			cmpi.b	#7,([$a78],d4.w*2,3)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([bd,an],od)'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$66660000,a4
			cmpi.b	#3,([-$66660000,a4],3)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([bd,an],xn.w*2)'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$66660000,a4
			move.l	#$66660002,d4
			cmpi.b	#4,([-$66660000,a4],d4.w*2)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([bd,an,xn.w*2],od)'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$66666666,a4
			move.l	#$11111111,d4
			cmpi.b	#12,([-$66666666-$2222,a4,d4.w*2],12)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([an,xn.w*2],od)'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$6666,a4
			move.l	#-$66666666/2,d4
			cmpi.b	#12,([a4,d4.w*2],12)
			jsr		TestZ1
			jsr		TestClose

			pea		#'([bd,an,xn.w*2])'
			jsr		TestOpen
			lea		bigtabptr,a4
			adda.l	#$66666666,a4
			move.l	#$11111111,d4
			cmpi.w	#$0001,([-$66666666-$2222,a4,d4.w*2])
			jsr		TestZ1
			jsr		TestClose

			jsr		TestPlan
			rts

			END
