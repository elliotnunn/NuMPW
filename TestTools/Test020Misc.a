    include 'Header.a'

    machine 68020

    StartOfTest 'extb.l positive'
    move.l  #$ffffff7f,d3
    move    #$f,ccr
    extb.l  d3
    n0
    z0
    v0
    c0
    cmp.l   #$0000007f,d3
    z1
    EndOfTest

    StartOfTest 'extb.l negative'
    move.l  #$00ff00f0,d3
    move    #$f,ccr
    extb.l  d3
    n1
    z0
    v0
    c0
    cmp.l   #$fffffff0,d3
    z1
    EndOfTest

    StartOfTest 'extb.l zero'
    move.l  #$00ff0000,d3
    move    #$f,ccr
    extb.l  d3
    n0
    z1
    v0
    c0
    cmp.l   #$00000000,d3
    z1
    EndOfTest

    StartOfTest 'link.l a6,#imm'
    move.l  #$12345678,a6
    lea     -4(sp),a0
    move.l  a6,a1
    lea     -14(sp),a2
    dc.w    $480E ; link.l a6,#imm, prevent assembler from optimising
    dc.l    -10
    cmp.l   a6,a0
    z1
    cmp.l   (a6),a1
    z1
    cmp.l   sp,a2
    z1
    add.w   #14,sp
    EndOfTest

    StartOfTest 'rtd positive'
    move.l  sp,a3 ; save sp
    lea     -100(sp),sp
    pea     .rtdHere
    rtd     #20
.rtdHere
    lea     -80(a3),a0
    cmp.l   a0,sp
    z1
    move.l  a3,sp
    EndOfTest

    StartOfTest 'rtd negative'
    move.l  sp,a3 ; save sp
    lea     -100(sp),sp
    pea     .rtdHere
    rtd     #-20
.rtdHere
    lea     -120(a3),a0
    cmp.l   a0,sp
    z1
    move.l  a3,sp
    EndOfTest

    StartOfTest 'pack dn,dn'
    move.l  #$ffff0607,d5
    clr.l   d6
    pack    d5,d6,#$0200
    pack    d5,d5,#$0200
    cmp.l   #$00000087,d6
    z1
    cmp.l   #$ffff0687,d5
    z1
    EndOfTest

    StartOfTest 'pack -(an),-(an)'
    lea      -100(a5),a4
    lea      -200(a5),a3
    move.w   #$0102,(a3)+
    pack     -(a3),-(a4),#$0304
    cmp.b    #$46,(a4)
    z1
    EndOfTest

    StartOfTest 'unpk dn,dn'
    move.l  #$ffffff67,d5
    clr.l   d6
    unpk    d5,d6,#$0200
    unpk    d5,d5,#$0200
    cmp.l   #$00000807,d6
    z1
    cmp.l   #$ffff0807,d5
    z1
    EndOfTest

    StartOfTest 'unpk -(an),-(an)'
    lea      -100(a5),a4
    lea      -200(a5),a3
    move.b   #$12,(a3)+
    unpk     -(a3),-(a4),#$0304
    cmp.w    #$0406,(a4)
    z1
    EndOfTest

    PrintPlan
    rts
